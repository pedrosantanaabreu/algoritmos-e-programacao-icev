/*
@Pedro Santana Abreu (https://linktr.ee/pedrosantanaabreu)
@Icev (https://somosicev.com)

PT-BR:
Desenvolva um programa para ser usado na eleição para
presidente do Centro Acadêmico de Engenharia de Software do
iCEV. O programa deve permitir que os alunos votem nos
candidatos de acordo com os números apresentados na tabela
abaixo. Caso o aluno queira votar nulo, ele deve informar o
número 98. Por outro lado, se ele quiser votar em branco, deve
informar o número 99.
Ao final da votação, o programa deve exibir:
• o total de votos para cada candidato;
• o total de votos nulos;
• o total de votos em branco;
• a porcentagem de votos nulos sobre o
total de votos; e
• a porcentagem de votos em branco
sobre o total de votos.
Para finalizar a votação, o mesário tem que informar o valor
99999 e, para números inválidos de candidato, o programa
deverá mostrar uma mensagem.

Número candidato
10 - João
20 - Maria
30 - Francisco
40 - Raimunda
*/

programa
{
	
	/*
	 * Biblioteca Matematica será utilizada para arredondar números
	 * Biblioteca Tipos será utilizada para transformar real para inteiro e cadeia de caracteres para inteiro
	 */
	 
	inclua biblioteca Matematica
	inclua biblioteca Tipos
	
	funcao inicio()
	{
		
		/* 
		 * Resolução problema verificação de voto válido:
		 * 	- Este problema pode ser resolvido de diversas formas, mas no caso desse programa a maneira escolhida... 
		 * 	...foi transformando o voto do usuario em cadeia de caracteres.
		 * 	- Permitindo o usuário digitar letras e símbolos sem comprometer a integridade do programa.
		 * 	
		 * 	Exemplo de aplicação:
		 * 		Sendo número:
		 * 			inteiro numero
		 * 			leia(numero)
		 * 			(usuário) -> A
		 * 			(error / programa fecha)
		 * 			
		 * 			// 'A' não é inteiro e nem pode ser convertido para inteiro, logo causando erro no programa 
		 *		
		 *		Sendo cadeia de caracteres:
		 *			cadeia numero
		 *			leia(numero)
		 *			(usuário) -> A
		 *			(programa realiza as comparações necessárias e caso nenhuma seja atendida ele exibe...
		 *			...uma mensagem de valor inválido e pede ao usuário digitar novamente)
		 */

		/*
		 * "voto_usuario" será a variavel cadeia que será recebida pelo programa
		 * "inteiro_voto_usuario" tera o valor de "voto_usuario", porem, em inteiro
		 */
		 
		cadeia voto_usuario = ""
		inteiro inteiro_voto_usuario = 0

		/*
		 * Tabela de candidatos e seus respectios números
		 */
		 
		inteiro numero_joao = 10
		inteiro numero_maria = 20
		inteiro numero_francisco = 30
		inteiro numero_raimunda = 40
		inteiro numero_nulo = 98
		inteiro numero_branco = 99
		inteiro numero_sair = 99999

		cadeia opcao_de_voto_escolhida = ""
		cadeia confirmacao_voto = ""
		
		logico verificador_voto_valido = falso
		logico voto_validado = falso
		
		/*
		 * Votos serão inicializados como real para que haja a possibilidade de divisão para realizar o calculo...
		 * ...de porcentagem
		 * 
		 * Logo em seguida todos serão transformados para inteiros para ser exibido no escreva final
		 */
		 
		real votos_candidato_joao = 0.0
		real votos_candidato_maria = 0.0
		real votos_candidato_francisco = 0.0
		real votos_candidato_raimunda = 0.0
		real votos_nulo = 0.0
		real votos_branco = 0.0
		real quantidade_total_votos = 0.0

		inteiro inteiro_votos_candidato_joao = 0
		inteiro inteiro_votos_candidato_maria = 0
		inteiro inteiro_votos_candidato_francisco = 0
		inteiro inteiro_votos_candidato_raimunda = 0
		inteiro inteiro_votos_nulo = 0
		inteiro inteiro_votos_branco = 0
		inteiro inteiro_quantidade_total_votos = 0
		
		real porcentagem_joao = 0.0
		real porcentagem_maria = 0.0
		real porcentagem_francisco = 0.0
		real porcentagem_raimunda = 0.0
		real porcentagem_nulo = 0.0
		real porcentagem_branco = 0.0
		
		/*
		 * 'contador_voto' Representa em que voto estamos
		 */
		 
		inteiro contador_voto = 1

		/*
		 * Usa-se enquanto (verdadeiro) para entrar pelo menos uma vez no loop...
		 * ...metodo semelhante ao faca...enquanto porém, o faca...enquanto não é recomendado a se trabalhar
		 */
		 
		enquanto (verdadeiro){
			escreva("=============== Tabela candidatos ===============",
			"\n", "[ ", numero_joao, " ]", " João",
			"\n", "[ ", numero_maria, " ]", " Maria",
			"\n", "[ ", numero_francisco, " ]", " Francisco",
			"\n", "[ ", numero_raimunda, " ]", " Raimunda",
			"\n", "[ ", numero_nulo, " ]", " Nulo",
			"\n", "[ ", numero_branco, " ]", " Branco",
			"\n",
			"\n", "[ ", numero_sair, " ]", " Sair",
			"\n")

			escreva("\n[ / ] ", contador_voto, "º voto",
			"\n[ ? ] Digite o número da opção",
			"\n[ > ] ")
			leia(voto_usuario)

			se (Tipos.cadeia_e_inteiro(voto_usuario, 10)){
				
				/*
				 * Convertendo cadeia de caracteres para inteiro
				 */
				 
				inteiro_voto_usuario = Tipos.cadeia_para_inteiro(voto_usuario, 10)
				
				se (inteiro_voto_usuario != numero_sair){
					se (inteiro_voto_usuario == numero_joao){
						verificador_voto_valido = verdadeiro
						opcao_de_voto_escolhida = "João"
					}
					
					senao se (inteiro_voto_usuario == numero_maria){
						verificador_voto_valido = verdadeiro
						opcao_de_voto_escolhida = "Maria"
					}
					
					senao se (inteiro_voto_usuario == numero_francisco){
						verificador_voto_valido = verdadeiro
						opcao_de_voto_escolhida = "Francisco"
					}
					
					senao se (inteiro_voto_usuario == numero_raimunda){
						verificador_voto_valido = verdadeiro
						opcao_de_voto_escolhida = "Raimunda"
					}
					
					senao se (inteiro_voto_usuario == numero_nulo){
						verificador_voto_valido = verdadeiro
						opcao_de_voto_escolhida = "Nulo"
					}
					
					senao se (inteiro_voto_usuario == numero_branco){
						verificador_voto_valido = verdadeiro
						opcao_de_voto_escolhida = "Branco"
					}
	
					senao {
						verificador_voto_valido = falso
						escreva("\n[ ! ] Valor inserido inválido, tente novamente.\n\n")
					}
				}
				
				senao {
					
					/*
					 * Confere se a quantidade de votos é maior que 0 visando evitar divisão por zero
					 */
					 
					se (quantidade_total_votos > 0){
						porcentagem_joao =  votos_candidato_joao / quantidade_total_votos * 100
						porcentagem_maria = votos_candidato_maria / quantidade_total_votos * 100
						porcentagem_francisco = votos_candidato_francisco / quantidade_total_votos * 100
						porcentagem_raimunda = votos_candidato_raimunda / quantidade_total_votos * 100
						porcentagem_nulo = votos_nulo / quantidade_total_votos * 100
						porcentagem_branco = votos_branco / quantidade_total_votos * 100
	
						porcentagem_joao = Matematica.arredondar(porcentagem_joao, 2)
						porcentagem_maria = Matematica.arredondar(porcentagem_maria, 2)
						porcentagem_francisco = Matematica.arredondar(porcentagem_francisco, 2)
						porcentagem_raimunda = Matematica.arredondar(porcentagem_raimunda, 2)
						porcentagem_nulo = Matematica.arredondar(porcentagem_nulo, 2)
						porcentagem_branco = Matematica.arredondar(porcentagem_branco, 2)

						/*
						 * Convertendo valores reais para inteiros
						 */
						 
						inteiro_votos_candidato_joao = Tipos.real_para_inteiro(votos_candidato_joao)
						inteiro_votos_candidato_maria = Tipos.real_para_inteiro(votos_candidato_maria)
						inteiro_votos_candidato_francisco = Tipos.real_para_inteiro(votos_candidato_francisco)
						inteiro_votos_candidato_raimunda = Tipos.real_para_inteiro(votos_candidato_raimunda)
						inteiro_votos_nulo = Tipos.real_para_inteiro(votos_nulo)
						inteiro_votos_branco = Tipos.real_para_inteiro(votos_branco)
						inteiro_quantidade_total_votos = Tipos.real_para_inteiro(quantidade_total_votos)
					}
					
					pare
				}
			}

			senao {
				verificador_voto_valido = falso
				escreva("\n[ ! ] Valor inserido inválido, tente novamente.\n\n")
			}

			/*
			 * Confirmação de voto
			 */
		
			se (verificador_voto_valido){
				enquanto (verdadeiro){
					escreva("\n=============== Confirmação de voto ===============",
					"\n[ / ] Opção | ", opcao_de_voto_escolhida,
					"\n[ / ] Número | ", voto_usuario,
					"\n",
					"\n[ S ] Para confirmar",
					"\n[ N ] Para recusar",
					"\n[ > ] ")
					leia(confirmacao_voto)
					
					se (confirmacao_voto == "S" ou confirmacao_voto == "s"){
						voto_validado = verdadeiro
						escreva("\n[ / ] Voto validado\n\n")
						pare
					}

					senao se (confirmacao_voto == "N" ou confirmacao_voto == "n"){
						voto_validado = falso
						escreva("\n[ / ] Voto cancelado\n\n")
						pare
					}

					senao {
						voto_validado = falso
						escreva("\n[ ! ] Valor inserido inválido, tente novamente.\n")
					}
				}

				/*
				 * Se o voto for validado pelo usuário, o voto será computado
				 */
				 
				se (voto_validado){
					quantidade_total_votos++

					se (inteiro_voto_usuario == numero_joao){
						votos_candidato_joao++
					}
					
					senao se (inteiro_voto_usuario == numero_maria){
						votos_candidato_maria++
					}

					senao se (inteiro_voto_usuario == numero_francisco){
						votos_candidato_francisco++
					}

					senao se (inteiro_voto_usuario == numero_raimunda){
						votos_candidato_raimunda++
					}

					senao se (inteiro_voto_usuario == numero_nulo){
						votos_nulo++
					}

					senao {
						votos_branco++
					}

				contador_voto++
				}
			}
		}

		escreva("\n=============== Resultados ===============",
		"\n[ ", inteiro_quantidade_total_votos, " ] | Total de votos\n",
		"\n[ ", inteiro_votos_candidato_joao," ] | [ ", porcentagem_joao, "% ] | João",
		"\n[ ", inteiro_votos_candidato_maria," ] | [ ", porcentagem_maria, "% ] | Maria",
		"\n[ ", inteiro_votos_candidato_francisco," ] | [ ", porcentagem_francisco, "% ] | Francisco",
		"\n[ ", inteiro_votos_candidato_raimunda," ] | [ ", porcentagem_raimunda, "% ] | Raimunda",
		"\n[ ", inteiro_votos_nulo," ] | [ ", porcentagem_nulo, "% ] | Nulo",
		"\n[ ", inteiro_votos_branco," ] | [ ", porcentagem_branco, "% ] | Branco")
	}
}
