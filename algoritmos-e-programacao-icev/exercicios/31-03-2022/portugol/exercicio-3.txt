/*
@Pedro Santana Abreu (https://linktr.ee/pedrosantanaabreu)
@Icev (https://somosicev.com)

PT-BR:
Desenvolva um programa que receba o valor do salário mínimo, o
número de horas trabalhadas, o número de dependentes do funcionário
e a quantidade de horas extras trabalhadas. Calcule e mostre o salário
a receber do funcionário de acordo com as regras a seguir:

a) O valor da hora trabalhada é igual a 1/5 do salário mínimo.

b) O salário do mês é igual ao número de horas trabalhadas multiplicado
pelo valor da hora trabalhada.

c) Para cada dependente, acrescentar R$ 32,00.

d) Para cada hora extra trabalhada, calcular o valor da hora trabalhada
acrescida de 50%.

e) O salário bruto é igual ao salário do mês
mais o valor dos dependentes mais o
valor das horas extras.

f) Calcular o valor do imposto de renda
retido na fonte de acordo com a tabela
ao lado.

g) O salário líquido é igual ao salário bruto
menos IRRF.

h) A gratificação é de acordo com a tabela
ao lado.
*/

programa
{

	inclua biblioteca Matematica --> mat
	
	funcao inicio()
	{

		// Recebendo valores
		inteiro horas, dependentes, horas_extras
		real salario, valor_hora, valor_hora_extra, salario_receber, valor_dependentes, valor_imposto, valor_bonus
		
		escreva("Informe o valor do salário mínimo | R$ ")
		leia(salario)
		escreva("Informe a quantidade de horas trabalhadas (h) | ") 
		leia(horas) 
		escreva("Informe a quantidade de dependentes | ")
		leia(dependentes)
		escreva("Informe a quantidade de horas extras trabalhadas (h) | ")
		leia(horas_extras)

		// Calculando
		valor_hora = salario / 5
		valor_hora_extra = valor_hora * 1.5
		salario_receber = horas * valor_hora
		salario_receber += dependentes * 32
		salario_receber += horas_extras * valor_hora_extra
		salario_receber = mat.arredondar(salario_receber, 2)
		
		// Exibindo resultado
		escreva("\nValor do salário bruto | R$ ", salario_receber)

		se (salario_receber < 200){
			valor_imposto = 0.0	
		}
		senao{
			se (salario_receber <= 500){
				valor_imposto = salario_receber * 10 / 100 
			}
			senao {
				valor_imposto = salario_receber * 20 / 100
			}
		}

		valor_imposto = mat.arredondar(valor_imposto, 2)
		escreva("\nValor do imposto | R$ ", valor_imposto)
		salario_receber -= valor_imposto

		se (salario_receber <= 350.0){
			valor_bonus = 100.0
		}
		senao{
			valor_bonus = 50.0
		}

		valor_bonus = mat.arredondar(valor_bonus, 2)
		escreva("\nValor do bônus | R$ ", valor_bonus)
		salario_receber += valor_bonus
		salario_receber = mat.arredondar(salario_receber, 2)
		escreva("\nValor do salário a receber | R$ ", salario_receber)
	}
}
